# This file is configured at cmake time, and loaded at cpack time.
# To pass variables to cpack from cmake, they must be configured
# in this file.

set (GIT_SHA_SHORT "@GIT_SHA_SHORT@")

set (CPACK_PACKAGE_NAME "@CMAKE_PROJECT_NAME@@CPACK_PACKAGE_SUFFIX@")
set (CPACK_PACKAGE_VERSION_MAJOR "@CMAKE_PROJECT_VERSION_MAJOR@")
set (CPACK_PACKAGE_VERSION_MINOR "@CMAKE_PROJECT_VERSION_MINOR@")
set (CPACK_PACKAGE_VERSION_PATCH "@CMAKE_PROJECT_VERSION_PATCH@")
set (CPACK_PACKAGE_VERSION_TWEAK "@CMAKE_PROJECT_VERSION_TWEAK@")
set (CPACK_PACKAGE_VERSION "@CMAKE_PROJECT_VERSION_MAJOR@.@CMAKE_PROJECT_VERSION_MINOR@.@CMAKE_PROJECT_VERSION_PATCH@")
if (CPACK_PACKAGE_VERSION_TWEAK)
  set (CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION}.wip@CMAKE_PROJECT_VERSION_TWEAK@~git@GIT_SHA_SHORT@")
endif ()

set (CPACK_SOURCE_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-src")
option (CPACK_SOURCE_TZ "" "OFF")

if (CPACK_GENERATOR MATCHES "DEB")
  if (NOT CPACK_DEBIAN_PACKAGE_RELEASE)
    set (CPACK_DEBIAN_PACKAGE_RELEASE "0${CPACK_SYSTEM_NAME}")
  endif ()
  set (CPACK_DEBIAN_FILE_NAME "DEB-DEFAULT")
endif ()

if (CPACK_GENERATOR MATCHES "RPM")
  if (NOT CPACK_RPM_PACKAGE_RELEASE)
    set (CPACK_RPM_PACKAGE_RELEASE "0")
  endif ()
  set (CPACK_RPM_PACKAGE_RELEASE_DIST TRUE)
  set (CPACK_RPM_FILE_NAME "RPM-DEFAULT")
endif ()

if (CPACK_GENERATOR MATCHES "NuGet")
  set (CPACK_NUGET_PACKAGE_VERSION "@CMAKE_PROJECT_VERSION_MAJOR@.@CMAKE_PROJECT_VERSION_MINOR@.@CMAKE_PROJECT_VERSION_PATCH@")
  if (CPACK_PACKAGE_VERSION_TWEAK)
    # Using SemVer 2.0.0 this will be recognized as a prerelease package
    # and must be installed with ``nuget install cigma -Prerelease [-Source <optional\local\path>]``
    set (CPACK_NUGET_PACKAGE_VERSION "${CPACK_NUGET_PACKAGE_VERSION}-wip.@CMAKE_PROJECT_VERSION_TWEAK@.git@GIT_SHA_SHORT@")
  endif ()
endif ()

# APPEND PACKAGE-SPECIFIC INSTRUCTIONS BELOW

if (CPACK_SYSTEM_NAME MATCHES "raspbian.*")
  set (CPACK_DEBIAN_PACKAGE_DEPENDS "fonts-open-sans, libts0")
endif ()
